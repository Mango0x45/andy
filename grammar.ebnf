program = {cmdlist};
cmdlist = pipeline, {lop, pipeline}, end;
pipeline = cmd, {'|', cmd};
cmd = simple | compound | if | while;

simple = value, {value}, {redir};
compound = '{', program, '}';
if = 'if', cmdlist, '{', program, '}', [else];
else = 'else', ('{', program, '}' | if);
while = 'while', cmdlist, '{', program, '}';

redir =
	( '<'
	| '>'
	| '>!'
	| '>>'
	), value;
value = arg | string | list;

arg = (* One of more non-whitespace and -metachar characters unless backslash
         escaped *);

string = sstring, dstring;
sstring = (* /('+).+?\g1/ *);
dstring = (* C-style string allowing escape sequences and variables *);

list = '(', {value}, ')';

lop = '&&' | '||';
end = ';' | '\n';
