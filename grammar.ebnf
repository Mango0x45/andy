(* Anything between ‘??’ represents a regex.  Regex character classes are
   unicode aware (‘\s’ matches all unicode spaces) *)

program = cmdlist, {cmdlist};
cmdlist = pipeline, {lop, pipeline}, end;
pipeline = cmd, {'|', cmd};
cmd = simple | compound | if | while;

simple = value, {value}, {redir};
compound = '{', cmdlist, {cmdlist}, '}';
if = 'if', cmdlist, cmdlist, ['else', cmdlist];
while = 'while', cmdlist;

redir =
	( '<'
	| '>'
	| '>!'
	| '>>'
	), value;
value = arg | string;

string = '"', ?[^"]*?, '"';
arg = ?[^|><;\n\s]+?;
lop = '&&' | '||';
end = ';' | '\n';
